#!/usr/bin/gawk -f
# Converts variant of MediaWiki to DocBook
# Ben Lynn

func open_section(tag) {
    stack[stack_i] = tag
    stack_i = stack_i + 1
    print "<"tag">"
}

func close_section() {
    close_block();
    stack_i = stack_i - 1
    print "</"stack[stack_i]">"
}

func close_until(i) {
    close_block()
    while (stack_i > i) {
	close_section()
    }
}

func close_block() {
  if (block_state != "") {
      print "</"block_state">"
      block_state = ""
  }
}

func in_block(s) {
  if (block_state != s) {
    close_block()
    print "<"s">"
    block_state = s
  }
}

BEGIN {
    stack_i = 0
}

/^ *$/ {
    close_block()
    next
}

/^ *===.*===$/ {
    gsub(" *=== *","")
    close_until(2)
    open_section("section")
    print "<title>"$0"</title>"
    next
}

/^ *==.*==$/ {
    gsub(" *== *","")
    close_until(1)
    open_section("section")
    print "<title>"$0"</title>"
    next
}

/^ *=.*=$/ {
    gsub(" *= *","")
    close_until(0)
    open_section("chapter")
    print "<title>"$0"</title>"
    next
}

/\[\[/ {
    gsub("\\[\\[", "<ulink url=\"")
    gsub("]\\[", "\">")
    gsub("]]", "</ulink>")
}

/^ / {
    in_block("screen")
    print $0
    next
}

{
    in_block("para")
    print $0
}

END {
    close_until(0)
}
